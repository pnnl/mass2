#! /bin/sh
# -------------------------------------------------------------
# file: configure.in
# -------------------------------------------------------------
# -------------------------------------------------------------
# Battelle Memorial Institute
# Pacific Northwest Laboratory
# -------------------------------------------------------------
# -------------------------------------------------------------
# Created March 25, 2003 by William A. Perkins
# Last Change: Wed Oct  5 09:26:38 2011 by William A. Perkins <d3g096@PE10900.pnl.gov>
# -------------------------------------------------------------

# -------------------------------------------------------------
# Initialization
# -------------------------------------------------------------
AC_INIT(MASS2, 0.5)
AM_INIT_AUTOMAKE()
AC_CANONICAL_HOST()

# -------------------------------------------------------------
# Checks for Programs we need
# -------------------------------------------------------------
AC_PROG_INSTALL()
AC_LANG(Fortran)

# -------------------------------------------------------------
# We need an MPI compiler, so check for that first
# -------------------------------------------------------------

ACX_MPI([mpi_ok=1])
if test "x$mpi_ok" != "x1" ; then 
    AC_MSG_ERROR([Failed to find MPI or F90 MPI compiler], 10)
fi
FC="$MPIFC"
AC_MSG_NOTICE([Configuring parallel MASS2 using MPI])

dnl Make sure the compiler choice is passed to subdirectories
ac_configure_args="$ac_configure_args FC=$FC"

AX_F90_MODULE_EXTENSION()
if test x$ax_cv_f90_modext = x ; then
   AC_MSG_ERROR([unable to find f90 modules extension])
fi
AC_SUBST(F90MODEXT)
F90MODEXT=$ax_cv_f90_modext

AX_F90_MODULE_FLAG()
if test x$ax_cv_f90_modflag = x"not found" ; then
   AC_MSG_ERROR([unable to determine f90 modules flag])
fi
AC_SUBST(F90MODFLAG)
F90MODFLAG="$ax_cv_f90_modflag"

AC_ARG_VAR(FC_CPPFLAG, [Fortran compiler flag to run C-preprocessor])

# -------------------------------------------------------------
# Check CGNS library
# -------------------------------------------------------------
top_FCFLAGS="$FCFLAGS"
FCFLAGS="$top_FCFLAGS $FC_CPPFLAG"
ACX_CGNS77()
FCFLAGS="$top_FCFLAGS"

# -------------------------------------------------------------
# Check Global Arrays, version 5.0 required
# -------------------------------------------------------------
# Define and check the Global Arrays library

AC_SUBST(GA_FCFLAGS)
AC_SUBST(GA_LIBS)
AC_ARG_VAR(GA_MPILIBS, [extra libraries required by Global Arrays])

AC_ARG_WITH(global-arrays,
    AC_HELP_STRING([--with-global-arrays=dir], [Specify the Global Arrays directory]),
    GA_DIR=$withval,
    GA_DIR="")

# if no target is set, assume GA 5.0 or later which has a different
# set of libraries

GA_LIBS="-lga -larmci ${GA_MPILIBS} -lm"
if test -n "$GA_DIR"; then
   GA_FCFLAGS="$GA_FCFLAGS -I${GA_DIR}/include"
   GA_LIBS="-L${GA_DIR}/lib ${GA_LIBS}"
   if test -n "$GA_TARGET"; then
      GA_FCFLAGS="$GA_FCFLAGS -D${GA_TARGET}"
      GA_LIBS="-L${GA_DIR}/lib/${GA_TARGET} ${GA_LIBS}"
   fi
fi

# -------------------------------------------------------------
# Check PETSc
# -------------------------------------------------------------
petsc_ok=
ACX_PETSC([petsc_ok=1], [petsc_ok=0])
if test x$petsc_ok != x1; then
    AC_MSG_ERROR([A working PETSc version 3.x library is required])
fi

# -------------------------------------------------------------
# Output
# -------------------------------------------------------------
AC_CONFIG_FILES([
    Makefile
    util/Makefile
    util/cart_grid/Makefile
])
AC_CONFIG_SUBDIRS(time_series)
AC_OUTPUT()

